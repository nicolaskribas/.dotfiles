pkgbase=system-meta-pkgs
pkgname=('base-system' 'kahve-system' 'cimarron-system' 'warana-system')
arch=('x86_64')
pkgrel=0
pkgver=0
source=('loader.conf' 'arch.conf' 'arch-lts.conf'
	'booster.yaml' 'nvidia.conf'
	'home-ether.network' 'home-wlan.network' 'uni-ether.network' 'uni-wlan.network'
	'dashbinsh.hook'
	'systemd-boot.hook'
	'paccache-1.hook' 'paccache-2.hook'
	'pacman.conf' 'reflector.conf'
	'base-system.preset' 'kahve-system.preset' 'warana-system.preset' 'cimarron-system.preset'
	'journald.conf' 'wait-online-override.conf'
	'flatpak-update.service' 'flatpak-update.timer'
	'locale.conf' 'locale.gen'
	'sudoers'

	# warana
	'vconsole.conf'
	'greetd-config.toml'
	'logind-power-key-hibernate.conf'
	'tlp-performance.conf' 'tlp-battery-care.conf'
)

md5sums=('SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP'
	'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP'
	'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP'
	'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP'
	'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP'
	'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP'
	'SKIP'
)


package_kahve-system() {
	install=kahve-system.install
	depends=(base-system
		booster
		amd-ucode sof-firmware
		mesa libva-mesa-driver mesa-vdpau
		iwd wireless-regdb bluez bluez-utils blueman
	)
	echo 'kahve' | install -Dm0644 /dev/stdin "$pkgdir"/etc/hostname
	echo 'WIRELESS_REGDOM="BR"' | install -Dm0644 /dev/stdin "$pkgdir"/etc/conf.d/wireless-regdom
	install -Dm0644 uni-ether.network "$pkgdir"/etc/systemd/network/20-uni-ether.network
	install -Dm0644 uni-wlan.network "$pkgdir"/etc/systemd/network/20-uni-wlan.network

	install -Dm0644 kahve-system.preset "$pkgdir"/etc/systemd/system-preset/70-kahve-system.preset

	install -Dm0644 loader.conf "$pkgdir"/boot/loader/loader.conf
	install -Dm0644 arch.conf "$pkgdir"/boot/loader/entries/arch.conf
	install -Dm0644 arch-lts.conf "$pkgdir"/boot/loader/entries/arch-lts.conf
}

package_cimarron-system() {
	depends=(base-system
		booster
		amd-ucode
		nvidia nvidia-lts
		libva-nvidia-driver vulkan-validation-layers
	)
	echo 'cimarron' | install -Dm0644 /dev/stdin "$pkgdir"/etc/hostname
	install -Dm0644 home-ether.network "$pkgdir"/etc/systemd/network/20-home-ether.network
	install -Dm0644 home-wlan.network "$pkgdir"/etc/systemd/network/20-home-wlan.network

	install -Dm0644 cimarron-system.preset "$pkgdir"/etc/systemd/system-preset/70-cimarron-system.preset

	install -Dm0644 loader.conf "$pkgdir"/boot/loader/loader.conf
	install -Dm0644 arch.conf "$pkgdir"/boot/loader/entries/arch.conf
	install -Dm0644 arch-lts.conf "$pkgdir"/boot/loader/entries/arch-lts.conf

	install -Dm0644 booster.yaml "$pkgdir"/etc/booster.yaml
	install -Dm0644 nvidia.conf "$pkgdir"/etc/modprobe.d/nvidia.conf
}

package_warana-system() {
	install=warana-system.install
	depends=(base-system
		intel-ucode mesa vulkan-intel intel-media-driver sof-firmware
		iwd wireless-regdb bluez bluez-utils blueman
		mkinitcpio cryptsetup sbctl fprintd
		snapper snap-pac
	)
	echo 'warana' | install -Dm0644 /dev/stdin "$pkgdir"/etc/hostname
	echo 'WIRELESS_REGDOM="BR"' | install -Dm0644 /dev/stdin "$pkgdir"/etc/conf.d/wireless-regdom
	echo 'options i915 enable_guc=3' | install -Dm0644 /dev/stdin "$pkgdir"/etc/modprobe.d/i915.conf
	install -Dm0644 vconsole.conf "$pkgdir"/etc/vconsole.conf

	install -Dm0644 logind-power-key-hibernate.conf "$pkgdir"/etc/systemd/logind.conf.d/power-key-hibernate.conf
	install -Dm0644 warana-system.preset "$pkgdir"/etc/systemd/system-preset/70-warana-system.preset
	install -Dm0644 home-ether.network "$pkgdir"/etc/systemd/network/20-home-ether.network
	install -Dm0644 home-wlan.network "$pkgdir"/etc/systemd/network/20-home-wlan.network

	install -Dm0644 tlp-battery-care.conf "$pkgdir"/etc/tlp.d/20-battery-care.conf
}

package_base-system() {
	install=base-system.install
	depends=(
		base linux linux-lts linux-firmware btrfs-progs tlp
		greetd greetd-tuigreet

		sway swaybg swayidle swaylock waybar j4-dmenu-desktop wmenu
		brightnessctl playerctl mako slurp grim swappy
		lxqt-policykit lxqt-openssh-askpass
		flatpak xdg-desktop-portal-gtk xdg-desktop-portal-wlr
		foot foot-terminfo neovim nautilus
		sudo polkit rtkit
		xdg-user-dirs
		inter-font ttf-jetbrains-mono ttf-nerd-fonts-symbols-mono terminus-font
		noto-fonts noto-fonts-cjk noto-fonts-emoji
		gnome-themes-extra adwaita-icon-theme breeze-icons hicolor-icon-theme
		pipewire pipewire-audio pipewire-jack pipewire-pulse wireplumber noise-suppression-for-voice
		xorg-xwayland libnotify wl-clipboard cliphist xdg-utils mailcap

		tmux dash bash zsh zsh-completions zsh-syntax-highlighting
		less man-db man-pages
		git difftastic git-delta
		fzf chafa ripgrep ripgrep-all fd tree gdu
		gcc clang rustup python
		stylua shellcheck ruff pyright
		jq sqlite zola miniserve
		make base-devel gdb strace
		htop bpftop bpftrace

		iproute2 ethtool nftables
		iputils nmap ldns tcpdump tcpreplay
		curl rclone sshfs
		firewalld openssh cups cups-pdf

		texlive-meta texlive-langenglish texlive-langportuguese texlab

		virt-manager libvirt qemu-full dmidecode dnsmasq podman docker docker-buildx

		archlinux-contrib pacman-contrib reflector rebuild-detector lostfiles fwupd
		smartmontools
	)
	install -Dm0644 dashbinsh.hook "$pkgdir"/etc/pacman.d/hooks/dashbinsh.hook
	install -Dm0644 systemd-boot.hook "$pkgdir"/etc/pacman.d/hooks/95-systemd-boot.hook
	install -Dm0644 paccache-1.hook "$pkgdir"/etc/pacman.d/hooks/paccache-1.hook
	install -Dm0644 paccache-2.hook "$pkgdir"/etc/pacman.d/hooks/paccache-2.hook
	install -Dm0644 pacman.conf "$pkgdir"/etc/pacman.conf

	install -Dm0644 reflector.conf "$pkgdir"/etc/xdg/reflector/reflector.conf

	install -Dm0644 base-system.preset "$pkgdir"/etc/systemd/system-preset/80-base-system.preset

	install -Dm0644 tlp-performance.conf "$pkgdir"/etc/tlp.d/10-performance.conf

	install -Dm0644 journald.conf "$pkgdir"/etc/systemd/journald.conf.d/override.conf
	install -Dm0644 wait-online-override.conf "$pkgdir"/etc/systemd/system/systemd-networkd-wait-online.service.d/override.conf

	install -Dm0644 flatpak-update.service "$pkgdir"/etc/systemd/system/flatpak-update.service
	install -Dm0644 flatpak-update.timer "$pkgdir"/etc/systemd/system/flatpak-update.timer

	install -Dm0644 locale.conf "$pkgdir"/etc/locale.conf
	install -Dm0644 locale.gen "$pkgdir"/etc/locale.gen

	install -dm0750 "$pkgdir"/etc/sudoers.d
	install -Dm0440 sudoers "$pkgdir"/etc/sudoers.d/10-defaults

	install -Dm0644 greetd-config.toml "$pkgdir"/etc/greetd/config.toml
}

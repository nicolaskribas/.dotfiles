pkgname=('etcfiles-common' 'etcfiles-spillway' 'etcfiles-archbtw')
arch=('x86_64')
pkgrel=0
pkgver=0
source=('loader.conf' 'arch.conf' 'arch-lts.conf'
	'home-ether.network' 'home-wlan.network' 'uni-ether.network' 'uni-wlan.network'
	'dashbinsh.hook'
	'paccache-1.hook' 'paccache-2.hook'
	'pacman.conf' 'reflector.conf'
	'journald.conf' 'wait-online-override.conf'
	'flatpak-update.service' 'flatpak-update.timer'
	'locale.conf' 'locale.gen'
	'sudoers'
)

md5sums=('SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP'
	'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP'
	'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP'
	'SKIP' 'SKIP' 'SKIP' 'SKIP'
)


package_etcfiles-spillway() {
	install=spillway.install
	depends=('etcfiles-common'
		amd-ucode
		iwd bluez bluez-utils blueman
	)
	echo 'spillway' | install -Dm0644 /dev/stdin "$pkgdir"/etc/hostname
	install -Dm0644 uni-ether.network "$pkgdir"/etc/systemd/network/20-uni-ether.network
	install -Dm0644 uni-wlan.network "$pkgdir"/etc/systemd/network/20-uni-wlan.network
}

package_etcfiles-archbtw() {
	depends=('etcfiles-common'
		amd-ucode
	)
	echo 'archbtw' | install -Dm0644 /dev/stdin "$pkgdir"/etc/hostname
	install -Dm0644 home-ether.network "$pkgdir"/etc/systemd/network/20-home-ether.network
	install -Dm0644 home-wlan.network "$pkgdir"/etc/systemd/network/20-home-wlan.network
}

package_etcfiles-common() {
	install=etcfiles.install
	depends=(
		base linux linux-lts linux-firmware booster

		sway swaybg swayidle swaylock waybar j4-dmenu-desktop wmenu
		playerctl mako slurp grim swappy
		polkit-kde-agent lxqt-openssh-askpass
		inter-font noto-fonts noto-fonts-cjk noto-fonts-emoji ttf-jetbrains-mono ttf-nerd-fonts-symbols-mono
		gnome-themes-extra adwaita-icon-theme breeze-icons hicolor-icon-theme
		pipewire pipewire-audio pipewire-jack pipewire-pulse wireplumber noise-suppression-for-voice
		xorg-xwayland libnotify wl-clipboard rtkit polkit
		xdg-desktop-portal-gtk xdg-desktop-portal-wlr xdg-utils xdg-user-dirs
		flatpak foot foot-terminfo nautilus wireshark-qt

		tmux dash bash zsh zsh-completions zsh-syntax-highlighting
		gcc clang rustup python
		git make base-devel gdb strace
		sudo less man-db man-pages
		fzf ripgrep fd tree gdu
		tcpdump ldns nmap iputils iproute2 nftables curl firewalld openssh cups
		sqlite zola rclone miniserve
		neovim stylua

		texlive-meta texlive-langenglish texlive-langportuguese texlab

		virt-manager libvirt qemu-full dmidecode dnsmasq podman docker docker-buildx

		archlinux-contrib pacman-contrib reflector rebuild-detector lostfiles fwupd
	)
	install -Dm0644 loader.conf "$pkgdir"/boot/loader/loader.conf
	install -Dm0644 arch.conf "$pkgdir"/boot/loader/entries/arch.conf
	install -Dm0644 arch-lts.conf "$pkgdir"/boot/loader/entries/arch-lts.conf

	install -Dm0644 dashbinsh.hook "$pkgdir"/etc/pacman.d/hooks/dashbinsh.hook
	install -Dm0644 paccache-1.hook "$pkgdir"/etc/pacman.d/hooks/paccache-1.hook
	install -Dm0644 paccache-2.hook "$pkgdir"/etc/pacman.d/hooks/paccache-2.hook
	install -Dm0644 pacman.conf "$pkgdir"/etc/pacman.conf

	install -Dm0644 reflector.conf "$pkgdir"/etc/xdg/reflector/reflector.conf

	install -Dm0644 journald.conf "$pkgdir"/etc/systemd/journald.conf.d/override.conf
	install -Dm0644 wait-online-override.conf "$pkgdir"/etc/systemd/system/systemd-networkd-wait-online.service.d/override.conf

	install -Dm0644 flatpak-update.service "$pkgdir"/etc/systemd/system/flatpak-update.service
	install -Dm0644 flatpak-update.timer "$pkgdir"/etc/systemd/system/flatpak-update.timer

	install -Dm0644 locale.conf "$pkgdir"/etc/locale.conf
	install -Dm0644 locale.gen "$pkgdir"/etc/locale.gen

	install -dm0750 "$pkgdir"/etc/sudoers.d
	install -Dm0440 sudoers "$pkgdir"/etc/sudoers.d/10-defaults
}
